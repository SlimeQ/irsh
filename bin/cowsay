#!/usr/bin/fish

set self (basename (status -f))
argparse -n$self -xf,r n f= r l W= e= T= b d g p s t w y -- $argv >/dev/null ^&1
or begin
    printf "%s: Invalid commandline\n" $self >&2
    exit 1
end

set cows ($self -l | tail -n +2 | tr ' ' '\n')

if set -q _flag_r[1]
    set flag_f -f $cows[(math (random)%(count $cows)+1)]
else if set -q _flag_f
    for cow in $cows
        test $_flag_f = $cow
        and set flag_f -f $cow
        and break
    end
    if not set -q flag_f
        printf "%s: %s: Cow not found\n" $self $_flag_f >&2
        exit 1
    end
end

if begin set -q _flag_n[1]; and set -q argv[1]; end
    printf "%s: cannot mix -n and string arguments\n" $self >&2
    exit 1
end

set -q _flag_W; and set flag_W -W $_flag_W
set -q _flag_e; and set flag_e -e $_flag_e
set -q _flag_T; and set flag_T -T $_flag_T

if set -q _flag_l
    printf "%s" "$cows"
    exit 0
end

$self $_flag_n[1] $flag_f $flag_W $flag_e $flag_T \
    $_flag_b[1] $_flag_d[1] $_flag_g[1] $_flag_p[1] \
    $_flag_s[1] $_flag_t[1] $_flag_w[1] $_flag_y[1] -- $argv
