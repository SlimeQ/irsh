#!/usr/bin/fish

set self (basename (status -f))
argparse -n$self 'i/index=!_validate_int --min 0' -- $argv >/dev/null 2>&1
or begin
    printf "%s: Invalid commandline\n" $self >&2
    exit 1
end

set api 'https://api.urbandictionary.com/v0'

if set -q _flag_i
    set item $_flag_i
else
    set item 0
end

function clean
    tr -d \r | sed '/^\s*$/d'
end

if set -q argv[1]
    curl -sSL -G $api/define --data-urlencode "term=$argv" \
        | jq -r ".list[$item].definition" \
        | clean
else
    curl -sSL -G $api/random \
        | jq -r ".list[$item]|.word,.definition" \
        | clean
end
