#!/usr/bin/fish

# http://developer.wordnik.com/docs.html

set self (basename (status -f))
argparse -n$self -xd,t,a,r d t a r -- $argv >/dev/null 2>&1
or begin
    printf "%s: Invalid commandline\n" $self >&2
    exit 1
end

set api 'https://api.wordnik.com/v4'
function word
    curl -sSL -H 'api_key: '(cat etc/wordnik) "$api/$argv"
end

set -q _flag_d
and set def 1

set -q _flag_t
and set topex 1

set -q _flag_a
and set allex 1

set -q _flag_r
and set related 1

if test -z "$def" -a -z "$topex" -a -z "$allex" -a -z "$related"
    set def 1
end

if set -q argv[1]
    set term "$argv"
else
    set term (word words.json/randomWord | jq -r '.["word"]')
    printf "%s\n" $term
end

if test -n "$def"
    word "word.json/$term/definitions" | jq -r '.[]["text"]'
else if test -n "$topex"
    word "word.json/$term/topExample" | jq -r '.["text"]'
else if test -n "$allex"
    word "word.json/$term/examples" | jq -r '.["examples"][]["text"]'
else if test -n "$related"
    word "word.json/$term/relatedWords" | jq -r '.[]["words"][]'
end
