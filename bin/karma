#!/usr/bin/fish

. lib/sed.fish

set self (basename (status -f))
argparse -n$self v g r n= a= b= -- $argv >/dev/null ^&1
or begin
    printf "%s: Invalid commandline\n" $self >&2
    exit 1
end

if set -q _flag_g[1]
    set files var/karma/*
else
    set files var/karma/$CHAN
end

if set -q _flag_r[1]
    set grep_flags -cE
else
    set grep_flags -cxF
end

if set -q _flag_n
    set nick_regexp (sed_escape_regexp $_flag_n)
else
    set nick_regexp '\S\+'
end

if set -q _flag_a
    set after (date -d $_flag_a '+%s')
else
    set after 0
end

if set -q _flag_b
    set before (date -d $_flag_b '+%s')
else
    set before (date '+%s')
end

set plus  (awk "\$1 > $after && \$1 < $before { print }" $files \
            | sed -n "s/\S\+ $nick_regexp ++ \(.*\)/\1/p" \
            | grep $grep_flags "$argv")
set minus (awk "\$1 > $after && \$1 < $before { print }" $files \
            | sed -n "s/\S\+ $nick_regexp -- \(.*\)/\1/p" \
            | grep $grep_flags "$argv")
set total (math $plus-$minus)

if set -q _flag_v[1]
    printf "%s (+%s/-%s)\n" $total $plus $minus
else
    printf "%s" $total
end
